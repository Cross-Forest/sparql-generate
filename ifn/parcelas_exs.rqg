BASE <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/>
PREFIX ifn: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/>
PREFIX spo: <http://crossforest.eu/position/ontology/>
PREFIX smo: <http://crossforest.eu/measures/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

PREFIX plot: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/plot/>
PREFIX basalArea: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/basalArea/>
PREFIX volumeWithBark: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/volumeWithBark/>
PREFIX volumeWithoutBark: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/volumeWithoutBark/>
PREFIX infoSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/infoSpecies/>
PREFIX infoSpeciesPlot: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/infoSpeciesPlot/>
PREFIX infoSpeciesDetail: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/infoSpeciesDetail/>
PREFIX numberOfTrees: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/numberOfTrees/>
PREFIX unit: <http://crossforest.eu/measures/data/unit/>
PREFIX ifn-clase-diametrica: <https://datos.iepnb.es/kos/sector-publico/medio-ambiente/ifn-clase-diametrica/>

GENERATE {

	<plot/{?idPlot}> a ifn:Plot , <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Plot-{?Cla}{?sc}> ;
		ifn:containsSpeciesPlot <infoSpeciesPlot/{?idPlotSpecies}> ;
		ifn:containsSpeciesDetail <infoSpeciesDetail/{?idPlotSpecies}-{?CD}> ;
	.

	<infoSpeciesPlot/{?idPlotSpecies}> a ifn:InfoSpeciesPlot ;
			ifn:hasSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?species}> ;

	GENERATE {
		<infoSpeciesPlot/{?idPlotSpecies}>
			ifn:hasBasalAreaInM2byHA ?{ sum(?basalArea) } ;
			ifn:hasVolumeWithoutBarkInM3byHA ?{ sum(?volumeWithoutBark) } ;
			ifn:hasVolumeWithBarkInM3byHA ?{ sum(?volumeWithBark) } ;
			ifn:hasNumberOfTreesInUnitsByHA ?{ sum(?numerOfTrees) } ;
			ifn:hasBasalArea <basalArea/{?idPlotSpecies}> ;
			ifn:hasVolumeWithoutBark <volumeWithoutBark/{?idPlotSpecies}> ;
			ifn:hasVolumeWithBark <volumeWithBark/{?idPlotSpecies}> ;
			ifn:hasNumberOfTrees <numberOfTrees/{?idPlotSpecies}> ;
		.

		<basalArea/{?idPlotSpecies}> a ifn:BasalArea ;
			smo:hasValue ?{ sum(?basalArea) } ;
			smo:hasUnit unit:M2byHA ;
		.

		<volumeWithoutBark/{?idPlotSpecies}> a ifn:VolumeWithoutBark ;
			smo:hasValue ?{ sum(?volumeWithoutBark) } ;
			smo:hasUnit unit:M3byHA ;
		.

		<volumeWithBark/{?idPlotSpecies}> a ifn:VolumeWithBark ;
			smo:hasValue ?{ sum(?volumeWithBark) } ;
			smo:hasUnit unit:M3byHA ;
		.

		<numberOfTrees/{?idPlotSpecies}> a ifn:NumberOfTrees ;
			smo:hasValue ?{ sum(?numerOfTrees) } ;
			smo:hasUnit unit:UnitsByHA ;
		.
	} 
	GROUP BY ?idPlotSpecies
	.

	<infoSpeciesDetail/{?idPlotSpecies}-{?CD}> a ifn:InfoSpeciesDetalle ;
			ifn:hasSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?species}> ;
	.

	<infoSpeciesDetail/{?idPlotSpecies}-{?CD}>
		ifn:hasDiametricClass <https://datos.iepnb.es/kos/sector-publico/medio-ambiente/ifn-clase-diametrica/{?CD}> ;
		ifn:hasBasalAreaInM2byHA ?basalArea ;
		ifn:hasVolumeWithoutBarkInM3byHA ?volumeWithoutBark ;
		ifn:hasVolumeWithBarkInM3byHA ?volumeWithBark ;
		ifn:hasNumberOfTreesInUnitsByHA ?numerOfTrees ;
	.


}
SOURCE <urn:source> AS ?source
ITERATOR <http://w3id.org/sparql-generate/iter/CSV>(?source, true, "\"", ",", "\n",
	"Origen","Estrato","Estadillo","Cla","Subclase","Especie","CD","NPies","ABas","VCC","VSC","IAVC","VLE"
	)
	AS ?Origen ?Estrato ?Estadillo ?Cla ?Subclase ?Especie ?CD ?NPies ?ABas ?VCC ?VSC ?IAVC ?VLE

WHERE {
	BIND( IF( BOUND( ?Subclase ), ?Subclase, "") AS ?sc )
	BIND("{?Origen}-{?Estadillo}-{?Cla}-{?sc}" AS ?idPlot)
	BIND("{?Origen}-{?Estadillo}-{?Cla}-{?sc}-{?Especie}" AS ?idPlotSpecies)
	BIND(xsd:decimal(?NPies) AS ?numerOfTrees)
	BIND(xsd:decimal(?ABas) AS ?basalArea)
	BIND(xsd:decimal(?VSC) AS ?volumeWithoutBark)
	BIND(xsd:decimal(?VCC) AS ?volumeWithBark)
	BIND(replace(?Especie, "0*([1-9][0-9]*)", "$1") AS ?species)
	# BIND(?numberOfTrees*?area AS ?absoluteNumberOfTrees)
	# BIND(?basalArea*?area AS ?absoluteBasalArea)
	# BIND(?volumeWithBark*?area AS ?absoluteVolumeWithBark)
}
