BASE <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/>
PREFIX ifn: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/>
PREFIX spo: <http://crossforest.eu/position/ontology/>
PREFIX smo: <http://crossforest.eu/measures/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

PREFIX plot: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/plot/>
PREFIX province:   <https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/>
PREFIX basalArea: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/basalArea/>
PREFIX volumeWithBark: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/volumeWithBark/>
PREFIX volumeWithoutBark: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/volumeWithoutBark/>
PREFIX infoSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/infoSpecies/>
PREFIX numberOfTrees: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/numberOfTrees/>
PREFIX unit: <http://crossforest.eu/measures/data/unit/>

GENERATE {

	<https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/{?Origen}> a ifn:Province ;
		ifn:containsSpecies <infoSpecies/{?idProvinceSpecies}> ;
	.

	<infoSpecies/{?idProvinceSpecies}> a ifn:InfoSpecies ;
			ifn:hasSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?species}> ;

	GENERATE {
		<infoSpecies/{?idProvinceSpecies}>
			ifn:hasBasalAreaInM2 ?{ sum(?basalArea*?surface) } ;
			ifn:hasVolumeWithoutBarkInM3 ?{ sum(?volumeWithoutBark*?surface) } ;
			ifn:hasVolumeWithBarkInM3 ?{ sum(?volumeWithBark*?surface) } ;
			ifn:hasNumberOfTreesInUnits ?{ sum(?numerOfTrees*?surface) } ;
			ifn:hasBasalArea <basalArea/{?idProvinceSpecies}> ;
			ifn:hasVolumeWithBark <volumeWithBark/{?idProvinceSpecies}> ;
			ifn:hasVolumeWithoutBark <volumeWithoutBark/{?idProvinceSpecies}> ;
			ifn:hasNumberOfTrees <numberOfTrees/{?idProvinceSpecies}> ;
		.

		<basalArea/{?idProvinceSpecies}> a ifn:BasalArea ;
			smo:hasValue ?{sum(?basalArea*?surface)} ;
			smo:hasUnit unit:M2 ;
		.

		<volumeWithoutBark/{?idProvinceSpecies}> a ifn:VolumeWithoutBark ;
			smo:hasValue ?{sum(?volumeWithoutBark*?surface)} ;
			smo:hasUnit unit:M3 ;
		.

		<volumeWithBark/{?idProvinceSpecies}> a ifn:VolumeWithBark ;
			smo:hasValue ?{sum(?volumeWithBark*?surface)} ;
			smo:hasUnit unit:M3 ;
		.

		<numberOfTrees/{?idProvinceSpecies}> a ifn:NumberOfTrees ;
			smo:hasValue ?{sum(?numerOfTrees*?surface)} ;
			smo:hasUnit unit:Units ;
		.
	} 
	GROUP BY ?idProvinceSpecies
	.

	GENERATE {
		<https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/{?Origen}> a ifn:Province ;
			ifn:hasBasalAreaInM2 ?{ sum(?basalArea*?surface) } ;
			ifn:hasVolumeWithoutBarkInM3 ?{ sum(?volumeWithoutBark*?surface) } ;
			ifn:hasVolumeWithBarkInM3 ?{ sum(?volumeWithBark*?surface) } ;
			ifn:hasNumberOfTreesInUnits ?{ sum(?numerOfTrees*?surface) } ;
			ifn:hasBasalArea <basalArea/{?Origen}> ;
			ifn:hasVolumeWithBark <volumeWithBark/{?Origen}> ;
			ifn:hasVolumeWithoutBark <volumeWithoutBark/{?Origen}> ;
			ifn:hasNumberOfTrees <numberOfTrees/{?Origen}> ;
		.

		<basalArea/{?Origen}> a ifn:BasalArea ;
			smo:hasValue ?{sum(?basalArea*?surface)} ;
			smo:hasUnit unit:M2 ;
		.

		<volumeWithoutBark/{?Origen}> a ifn:VolumeWithoutBark ;
			smo:hasValue ?{sum(?volumeWithoutBark*?surface)} ;
			smo:hasUnit unit:M3 ;
		.

		<volumeWithBark/{?Origen}> a ifn:VolumeWithBark ;
			smo:hasValue ?{sum(?volumeWithBark*?surface)} ;
			smo:hasUnit unit:M3 ;
		.

		<numberOfTrees/{?Origen}> a ifn:NumberOfTrees ;
			smo:hasValue ?{sum(?numerOfTrees*?surface)} ;
			smo:hasUnit unit:Units ;
		.
	} 
	GROUP BY ?Origen
	.
	
}
SOURCE <urn:source> AS ?source_exs
ITERATOR <http://w3id.org/sparql-generate/iter/CSV>(?source_exs, true, "\"", ",", "\n",
	"Origen","Estrato","Especie","CD","NPies","ABas","VCC","VSC","IAVC","VLE","NPar","Superficie","IdEspDom"
	)
	AS ?Origen ?Estrato ?Especie ?CD ?NPies ?ABas ?VCC ?VSC ?IAVC ?VLE ?NPar ?Superficie ?IdEspDom
WHERE {
	BIND("{?Origen}-{?Estrato}-{?Especie}" AS ?idProvinciaEstratoEspecie)
	BIND("{?Origen}-{?Estrato}" AS ?idProvinciaEstrato)
	BIND(iri("stratum/{?idProvinciaEstrato}") AS ?iriProvinciaEstrato)
	BIND("{?Origen}-{?Especie}" AS ?idProvinceSpecies)
	BIND(xsd:decimal(?NPies) AS ?numerOfTrees)
	BIND(xsd:decimal(?ABas) AS ?basalArea)
	BIND(xsd:decimal(?VSC) AS ?volumeWithoutBark)
	BIND(xsd:decimal(?VCC) AS ?volumeWithBark)
	BIND(xsd:decimal(?NPar) AS ?numberOfPlots)
	BIND(xsd:decimal(?Superficie) AS ?surface)
	BIND(replace(?Especie, "0*([1-9][0-9]*)", "$1") AS ?species)
}