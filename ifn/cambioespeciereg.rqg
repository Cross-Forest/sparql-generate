BASE <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/>
PREFIX ifn: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/>
PREFIX spo: <http://crossforest.eu/position/ontology/>
PREFIX smo: <http://crossforest.eu/measures/ontology/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

PREFIX province:   <http://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/>
PREFIX changeOfSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/changeOfSpecies/>
PREFIX numberOfTrees: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/numberOfTrees/>
PREFIX unit: <http://crossforest.eu/measures/data/unit/>

GENERATE {

	<http://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/{?Origen}> a ifn:Province ;
		ifn:hasChangeOfSpecies <changeOfSpecies/{?idCambioEspecies}> ;
	.

	<changeOfSpecies/{?idCambioEspecies}> a ifn:ChangeOfSpecies ;
		ifn:hasOriginalSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?oldSpecies}> ;
		ifn:hasNewSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?newSpecies}> ;
		ifn:hasNumberOfTrees <numberOfTrees/{?NumPies}> ;
		ifn:hasNumberOfTreesInUnits ?numberOfTrees ;
	.

	<numberOfTrees/{?NumPies}> a ifn:NumberOfTrees ;
		smo:hasUnit unit:Unities ;
		smo:hasValue ?numberOfTrees ;
	.
}
SOURCE <urn:source> AS ?source_exs
ITERATOR <http://w3id.org/sparql-generate/iter/CSV>(?source_exs, true, "\"", ",", "\n",
	"Origen", "EspOriginal", "EspCambiada", "NumPies"
	)
	AS ?Origen ?EspOriginal ?EspCambiada ?NumPies
WHERE {
	BIND("{?Origen}-{?EspOriginal}-{?EspCambiada}" AS ?idCambioEspecies)
	BIND(replace(?EspOriginal, "0*([1-9][0-9]*)", "$1") AS ?oldSpecies)
	BIND(replace(?EspCambiada, "0*([1-9][0-9]*)", "$1") AS ?newSpecies)
	BIND(IF("{?NumPies}"!="" && "{?NumPies}"!="0", xsd:integer(?NumPies), ?unbound) AS ?numberOfTrees)
}