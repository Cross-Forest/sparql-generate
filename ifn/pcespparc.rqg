BASE <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/>
PREFIX ifn: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/>
PREFIX smo: <http://crossforest.eu/measures/ontology/>
PREFIX spo: <http://crossforest.eu/position/ontology/>
PREFIX esadm: <http://vocab.linkeddata.es/datosabiertos/def/sector-publico/territorio#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

PREFIX plot: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/plot/>
PREFIX position: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/position/>
PREFIX province:   <https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/>
PREFIX infoSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/infoSpecies/>
PREFIX infoSpeciesPcEspParc: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/infoSpeciesPcEspParc/>
PREFIX occupationOfSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/occupationOfSpecies/>
PREFIX age: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/age/>
PREFIX source: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/source/>
PREFIX reliability: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/reliability/>
PREFIX origin: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/origin/>
PREFIX system: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/system/>
PREFIX unit: <http://crossforest.eu/measures/data/unit/>

PREFIX mfe-edd: <https://datos.iepnb.es/kos/sector-publico/medio-ambiente/mfe-estado-de-desarrollo/>


GENERATE {
	<plot/{?idPlot}> a ifn:Plot , <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Plot-{?Cla}{?sc}> ;
		spo:hasPosition <position/{?idPosition}> ;
		esadm:provincia <https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/{?Origen}> ;
		ifn:containsSpeciesPcEspParc <infoSpeciesPcEspParc/{?idPlotSpecies}> ;
	.

	<infoSpeciesPcEspParc/{?idPlotSpecies}> a ifn:InfoSpeciesPcEspParc ;
		ifn:hasSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?species}> ;
		ifn:hasStandDevelopment <https://datos.iepnb.es/kos/sector-publico/medio-ambiente/ifn-estado-de-desarrollo/{?STATE}> ;
		ifn:hasPlotShape <https://datos.iepnb.es/kos/sector-publico/medio-ambiente/ifn-forma-principal-de-masa/{?PLOT_SHAPE}> ;
		ifn:hasOccupationOfSpecies <occupationOfSpecies/{?OCCUPATION}> ;
		ifn:hasPercentageOfSpecies ?percentageOccupation ;
		ifn:hasAge <age/{?SOURCE}-{?RELIABILITY}-{?AGE}> ;
		ifn:hasAgeInYears ?ageInYears ;
		ifn:hasOrigin <origin/1{?ORIGIN1}> , <origin/2{?ORIGIN2}> ;
		ifn:hasSystem <system/{?SYSTEM}> ;
		ifn:hasOccupationOrder ?occupationOrder ;
	.

	<occupationOfSpecies/{?OCCUPATION}> a ifn:OccupationOfSpecies ;
		smo:hasValue ?percentageOccupation ;
		smo:hasUnit unit:Percentage ;
	.

	<age/{?SOURCE}-{?RELIABILITY}-{?AGE}> a ifn:Age ;
		smo:source  <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/source/{?SOURCE}> ;
		smo:Reliability <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/ifn/reliability/{?RELIABILITY}> ;
		smo:hasValue ?ageInYears ;
		smo:hasUnit unit:Years ;
	.
}
SOURCE <urn:source> AS ?source
ITERATOR <http://w3id.org/sparql-generate/iter/CSV>(?source, true, "\"", ",", "\n",
	"Origen", "Estadillo", "Cla", "Subclase", "PosEsp", "Especie", "Ocupa", "Estado", "FPMasa", "Edad", "FInfor", "Fiabil", "Barrena1", "Barrena2", "Barrena3", "AltPer", "OrgMasa1", "OrgMasa2", "TratMasa"
	)
	AS ?Origen ?Estadillo ?Cla ?Subclase ?PosEsp ?Especie ?Ocupa ?Estado ?FPMasa ?Edad ?FInfor ?Fiabil ?Barrena1 ?Barrena2 ?Barrena3 ?AltPer ?OrgMasa1 ?OrgMasa2 ?TratMasa
WHERE {
	BIND( IF( BOUND( ?Subclase ), ?Subclase, "") AS ?sc )
	BIND("{?Origen}-{?Estadillo}-{?Cla}-{?sc}" AS ?idPlot)
	BIND("{?Origen}-{?Estadillo}" AS ?idPosition)
	BIND(replace(?Especie, "0*([1-9][0-9]*)", "$1") AS ?species)
	BIND("{?Origen}-{?Estadillo}-{?Cla}-{?sc}-{?species}-p{?PosEsp}" AS ?idPlotSpecies)
	BIND(xsd:decimal(?PosEsp) AS ?occupationOrder)
	BIND(xsd:decimal(?Ocupa)*10 AS ?percentageOccupation)
	BIND("{?Ocupa}" AS ?OCCUPATION)
	BIND("{?Estado}" AS ?STATE)
	BIND("{?FPMasa}" AS ?PLOT_SHAPE)
	BIND(IF("{?Edad}"!="" && "{?Edad}"!="0", xsd:decimal(?Edad), ?unbound) AS ?ageInYears)
	BIND(IF("{?Edad}"!="" && "{?Edad}"!="0", "{?ageInYears}", ?unbound) AS ?AGE)
	BIND("{?FInfor}" AS ?SOURCE)
	BIND("{?Fiabil}" AS ?RELIABILITY)
	BIND("{?OrgMasa1}" AS ?ORIGIN1)
	BIND("{?OrgMasa2}" AS ?ORIGIN2)
	BIND("{?TratMasa}" AS ?SYSTEM)
}
