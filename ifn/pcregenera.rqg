BASE <http://crossforest.eu/ifn/data/>
PREFIX ifn: <http://crossforest.eu/ifn/ontology/>
PREFIX smo: <http://crossforest.eu/measures/ontology/>
PREFIX spo: <http://crossforest.eu/position/ontology/>
PREFIX esadm: <http://vocab.linkeddata.es/datosabiertos/def/sector-publico/territorio#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

PREFIX plot: <http://crossforest.eu/ifn/data/plot/>
PREFIX infoSpecies: <http://crossforest.eu/ifn/data/infoSpecies/>
PREFIX size: <http://crossforest.eu/ifn/data/size/>
PREFIX density: <http://crossforest.eu/ifn/data/density/>
PREFIX height:   <http://crossforest.eu/ifn/data/height/>
PREFIX unit: <http://crossforest.eu/measures/data/unit/>
PREFIX numberOfTrees: <http://crossforest.eu/ifn/data/numberOfTrees/>


GENERATE {
	<plot/{?idPlot}> a ifn:Plot , <http://crossforest.eu/ifn/ontology/Plot-{?Cla}{?sc}> ;
		ifn:containsSpecies <infoSpecies/{?idPlotSpecies}> ;
	.

	<infoSpecies/{?idPlotSpecies}> a ifn:InfoSpecies ;
		ifn:hasSpecies <http://crossforest.eu/ifn/ontology/Species{?species}> ;
		ifn:hasSizeCategory <size/{?CatDes}> ;
		ifn:hasDensity <density/{?Densidad}> ;
		ifn:hasNumberOfTrees <numberOfTrees/{?idPlotSpecies}> ;
		ifn:hasNumberOfTreesInUnits ?numPies ;
		ifn:hasAverageHeight <height/{?AVERAGE_HEIGHT}> ;
		ifn:hasAverageHeightInDecimeters ?averageHeight ;
	.

	<height/{?AVERAGE_HEIGHT}> a smo:MeasureInDecimeters ;
		smo:hasUnit unit:Decimeters ;
		smo:hasValue ?averageHeight ;
	.

	<numberOfTrees/{?idPlotSpecies}> a ifn:NumberOfTrees ;
		smo:hasValue ?numPies ;
		smo:hasUnit unit:UnitsByHA ;
	.

}
SOURCE <urn:source> AS ?source
ITERATOR <http://w3id.org/sparql-generate/iter/CSV>(?source, true, "\"", ",", "\n",
	"Origen", "Estadillo", "Especie", "Cla", "Subclase", "CatDes", "Tipo", "Densidad", "NumPies", "Hm"
	)
	AS ?Origen ?Estadillo ?Especie ?Cla ?Subclase ?CatDes ?Tipo ?Densidad ?NumPies ?Hm
WHERE {
	BIND( IF( BOUND( ?Subclase ), ?Subclase, "") AS ?sc )
	BIND("{?Origen}-{?Estadillo}-{?Cla}-{?sc}" AS ?idPlot)
	BIND(replace(?Especie, "0*([1-9][0-9]*)", "$1") AS ?species)
	BIND("{?Origen}-{?Estadillo}-{?Cla}-{?sc}-{?species}-d{?CatDes}" AS ?idPlotSpecies)
	BIND(IF("{?NumPies}"!="" && "{?NumPies}"!="0", xsd:integer(?NumPies), ?unbound) AS ?numPies)
	BIND(IF("{?Hm}"!="" && "{?Hm}"!="0", xsd:decimal(?Hm), ?unbound) AS ?averageHeight)
	BIND(IF(BOUND(?averageHeight), "{?averageHeight}", ?unbound) AS ?AVERAGE_HEIGHT)
}