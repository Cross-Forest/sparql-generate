BASE <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX ifn: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/>
PREFIX mfe: <https://datos.iepnb.es/def/sector-publico/medio-ambiente/mfe/>
PREFIX smo: <http://crossforest.eu/measures/ontology/>
PREFIX spo: <http://crossforest.eu/position/ontology/>
PREFIX epsg: <http://epsg.w3id.org/ontology/>
PREFIX esadm: <http://vocab.linkeddata.es/datosabiertos/def/sector-publico/territorio#>

PREFIX use:<http://crossforest.eu/cos/ontology/Use/>
PREFIX patch: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/patch/>
PREFIX polygon: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/polygon/>
PREFIX crs: <http://crossforest.eu/epsg/data/crs/>
PREFIX wkt: <http://www.opengis.net/ont/geosparql#>
PREFIX unit: <http://crossforest.eu/measures/data/unit/>
PREFIX infoSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/infoSpecies/>
PREFIX occupationOfSpecies: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/occupationOfSpecies/>
PREFIX infoPatch: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/infoPatch/>
PREFIX rateOfPatch: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/rateOfPatch/>
PREFIX infoUse: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/infoUse/>
PREFIX rateOfUse: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/rateOfUse/>

PREFIX layer: <http://crossforest.eu/ilu/data/layer/>
PREFIX province: <https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/>
PREFIX bound: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/bound/>
PREFIX area: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/area/>
PREFIX axis: <http://epsg.w3id.org/data/axis/>

PREFIX mfe-fcc: <https://datos.iepnb.es/kos/sector-publico/medio-ambiente/mfe-fraccion-cabida-cubierta/>

PREFIX mfe-recurso-fcc-arborea: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/fraccion-cabida-cubierta/arborea/>
PREFIX mfe-recurso-fcc-total: <https://datos.iepnb.es/recurso/sector-publico/medio-ambiente/mfe/fraccion-cabida-cubierta/total/>

GENERATE {

	<patch/{?idPatch}> a mfe:Patch ,
			<https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Use{?CLAS_IFN}> ;
		spo:hasPolygon <polygon/{?idPatch}-4326> ;
		mfe:hasCanopyCoverTotal <fraccion-cabida-cubierta/total/{?TFCCTOT}> ;
		mfe:hasCanopyCoverTotalPercent ?tfcctot ;
		mfe:hasCanopyCoverTrees <fraccion-cabida-cubierta/arborea/{?TFCCARB}> ;
		mfe:hasCanopyCoverTreesPercent ?tfccarb ;
		esadm:provincia <https://datos.iepnb.es/recurso/sector-publico/territorio/Provincia/{?province}> ;
	.

	<polygon/{?idPatch}-4326> a spo:Polygon ;
		spo:isInLayer <http://crossforest.eu/ilu/data/layer/{?MERGED_LAYER}> ;
		epsg:hasCoordinateReferenceSystem crs:4326 ;
		wkt:asWKT ?geometricCoordinates ;
		spo:hasLeftBound <bound/107-{?WEST_BOUND}> ;
		epsg:hasLeftBound107 ?west ;
		spo:hasLowerBound <bound/106-{?SOUTH_BOUND}> ;
		epsg:hasLowerBound106 ?south ;
		spo:hasRightBound <bound/107-{?EAST_BOUND}> ;
		epsg:hasRightBound107 ?east ;
		spo:hasUpperBound <bound/106-{?NORTH_BOUND}> ;
		epsg:hasUpperBound106 ?north ;
		spo:hasArea <area/{?SHAPE_AREA}-M2> ;
		spo:hasAreaInSquareMeters ?ShapeArea ;
	.

	<bound/107-{?WEST_BOUND}> a epsg:LeftBound ;
		epsg:hasAxis axis:107 ;
		smo:hasValue ?west ;
		smo:hasUnit unit:Degrees ;
	.

	<bound/106-{?SOUTH_BOUND}> a epsg:LowerBound ;
		epsg:hasAxis axis:106 ;
		smo:hasValue ?south ;
		smo:hasUnit unit:Degrees ;
	.

	<bound/107-{?EAST_BOUND}> a epsg:RightBound ;
		epsg:hasAxis axis:107 ;
		smo:hasValue ?east ;
		smo:hasUnit unit:Degrees ;
	.

	<bound/106-{?NORTH_BOUND}> a epsg:UpperBound ;
		epsg:hasAxis axis:106 ;
		smo:hasValue ?north ;
		smo:hasUnit unit:Degrees ;
	.

	<area/{?SHAPE_AREA}-M2> a smo:MeasureInSquareMeters ;
		smo:hasUnit unit:SquareMeters ;
		smo:hasValue ?ShapeArea ;
	.

	<fraccion-cabida-cubierta/total/{?TFCCTOT}> a mfe-fcc:total ;
		smo:hasUnit unit:Percentage ;
		smo:hasValue "{?TFCCTOT}"^^xsd:decimal ;
	.

	<fraccion-cabida-cubierta/arborea/{?TFCCARB}> a mfe-fcc:arborea ;
		smo:hasUnit unit:Percentage ;
		smo:hasValue "{?TFCCARB}"^^xsd:decimal ;
	.

	GENERATE {
    <patch/{?idPatch}> mfe:containsPatch <infoPatch/{?SUB_PATCH}-{?PERCENTAGE_PATCH}> ;
    	mfe:containsSpecies <infoSpecies/{?subSpecies}-{?PERCENTAGE_SPECIES}>
    .

		<infoPatch/{?SUB_PATCH}-{?PERCENTAGE_PATCH}> a mfe:PatchInPatch ;
			mfe:hasPatch <patch/{?SUB_PATCH}> ;
			mfe:hasPercentageOfPatch ?percentagePatch ;
			mfe:hasRateOfPatch <rateOfPatch/{?PERCENTAGE_PATCH}-100>
		.

		<rateOfPatch/{?PERCENTAGE_PATCH}-100> a mfe:RateofPatch ;
			smo:hasValue ?percentagePatch ;
			smo:hasUnit unit:Percentage ;
		.
	 }
  	ITERATOR iter:JSONPath(?subpolygons, "$.*", "$.POLIGON", "$.Shape_Area") AS ?subpolygon ?subpatch ?ShapeArea2
		WHERE {
			BIND("{?SUB_LAYER}-{?province}-{?subpatch}" AS ?SUB_PATCH)
	    	BIND(ROUND((xsd:decimal(?ShapeArea2)/?ShapeArea)*100) AS ?percentagePatch)
	    	BIND("{?percentagePatch}" AS ?PERCENTAGE_PATCH)
	 }
	 .

	 GENERATE {
		<patch/{?idPatch}> mfe:containsSpecies <infoSpecies/{?subSpecies}-{?PERCENTAJE_SPECIES}> .

		<infoSpecies/{?subSpecies}-{?PERCENTAJE_SPECIES}> a mfe:SpeciesInPatch ;
			mfe:hasSpecies <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Species{?subSpecies}> ;
			mfe:hasOccupationOfSpecies <occupationOfSpecies/{?PERCENTAJE_SPECIES}-100> ;
			mfe:hasPercentageOfSpecies ?percentageSpecies ;
		.

		<occupationOfSpecies/{?PERCENTAJE_SPECIES}-100> a mfe:OccupationOfSpecies ;
			smo:hasValue ?percentageSpecies ;
			smo:hasUnit unit:Percentage ;
		.


	}
	ITERATOR iter:JSONListKeys(?SPECIES) AS ?subSpecies
	WHERE {
		BIND(xsd:decimal(fun:JSONPath(?SPECIES,'$.{?subSpecies}')) AS ?percentageSpecies)
		BIND("{?percentageSpecies}" AS ?PERCENTAJE_SPECIES)
	}
	.

	GENERATE {
		<patch/{?idPatch}> mfe:containsUse <infoUse/{?subUse}-{?PERCENTAJE_USE}> .

		<infoUse/{?subUse}-{?PERCENTAJE_USE}> a mfe:UseInPatch ;
		 	mfe:hasUse <https://datos.iepnb.es/def/sector-publico/medio-ambiente/ifn/Use{?subUse}> ;
			mfe:hasPercentageOfUse ?percentageUse ;
			mfe:hasRateOfUse <rateOfUse/{?PERCENTAJE_USE}-100>
		.

		<rateOfUse/{?PERCENTAJE_USE}-100> a mfe:RateofUse ;
			smo:hasValue ?percentageUse ;
			smo:hasUnit unit:Percentage ;
		.
	}
	ITERATOR iter:JSONListKeys(?USES) AS ?subUse
	WHERE {
		BIND(xsd:decimal(fun:JSONPath(?USES,'$.{?subUse}')) AS ?percentageUse)
		BIND("{?percentageUse}" AS ?PERCENTAJE_USE)
	}
	.

}
SOURCE <urn:source> AS ?source
ITERATOR iter:GeoJSON(?source) AS ?geometricCoordinates ?properties
WHERE {
	BIND("s5" AS ?SUB_LAYER)
	BIND("s5s5" AS ?MERGED_LAYER)
	BIND(fun:JSONPath(?properties, "$.POLIGON") AS ?poligon)
	# BIND("{?MERGED_LAYER}-{?poligon}" AS ?POLIGON)
	BIND(fun:JSONPath(?properties, "$.CLAS_IFN") AS ?CLAS_IFN)
	BIND(fun:JSONPath(?properties, "$.TFCCTOT") AS ?tfcctot)
	BIND("{?tfcctot}" AS ?TFCCTOT)
	BIND(fun:JSONPath(?properties, "$.TFCCARB") AS ?tfccarb)
	BIND("{?tfccarb}" AS ?TFCCARB)
	BIND(fun:JSONPath(?properties, "$.PROV_MFE50") AS ?prov_mfe50)
	BIND("{?prov_mfe50}" AS ?PROV_MFE50)
	BIND(IF(STRLEN(?PROV_MFE50)=1,CONCAT("0",?PROV_MFE50),?PROV_MFE50) AS ?province)
	BIND(xsd:decimal(fun:JSONPath(?properties, "$.BBOX[0]")) AS ?west)
	BIND("{?west}" AS ?WEST_BOUND)
	BIND(xsd:decimal(fun:JSONPath(?properties, "$.BBOX[1]")) AS ?south)
	BIND("{?south}" AS ?SOUTH_BOUND)
	BIND(xsd:decimal(fun:JSONPath(?properties, "$.BBOX[2]")) AS ?east)
	BIND("{?east}" AS ?EAST_BOUND)
	BIND(xsd:decimal(fun:JSONPath(?properties, "$.BBOX[3]")) AS ?north)
	BIND("{?north}" AS ?NORTH_BOUND)
	BIND(xsd:decimal(fun:JSONPath(?properties, "$.Shape_Area")) AS ?ShapeArea)
	BIND("{?ShapeArea}" AS ?SHAPE_AREA)
	BIND(fun:JSONPath(?properties, "$.SUBPOLYGONS") AS ?subpolygons)
	BIND(fun:JSONPath(?properties, "$.SPECIES") AS ?SPECIES)
	BIND(fun:JSONPath(?properties, "$.USES") AS ?USES)
	BIND("{?MERGED_LAYER}-{?province}-{?poligon}" AS ?idPatch)
}
